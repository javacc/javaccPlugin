plugins {
    id 'javaccPlugin.plugin-conventions'

    id 'checkstyle'
    id 'com.github.spotbugs' version '4.7.1'
    id 'jacoco'
}

defaultTasks 'clean', 'build', 'install'

sourceSets {
    acceptanceTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    acceptanceTestImplementation.extendsFrom testImplementation
}

dependencies {
    implementation('commons-io:commons-io') {
        version { strictly '2.4' }
    }
    implementation 'org.apache.commons:commons-lang3:3.4'
    implementation 'org.apache.commons:commons-collections4:4.1'
    implementation 'com.google.guava:guava-jdk5:17.0'

    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.4'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.4'

    compileOnly        'net.java.dev.javacc:javacc:6.1.2'
    testImplementation 'net.java.dev.javacc:javacc:6.1.2'
}

eclipse {
    project {
        name = 'javacc-gradle-plugin'
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    manifest {
        attributes 'Implementation-Title': 'javacc-gradle-plugin', 'Implementation-Version': archiveVersion
    }
}

checkstyle {
    configFile = file("${rootProject.projectDir}/config/checkstyle/checks.xml")
}

spotbugs {
    toolVersion = '4.2.1'
    effort = "min"
    maxHeapSize = project.hasProperty('spotBugsHeapSize') ? project.spotBugsHeapSize : '1g'
    ignoreFailures = false
    reportLevel = "high"
    excludeFilter = file("${rootProject.projectDir}/config/findbugs/exclude.xml")
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

tasks.register('acceptanceTest', Test) {
    mustRunAfter test

    testClassesDirs = sourceSets.acceptanceTest.output.classesDirs
    classpath = sourceSets.acceptanceTest.runtimeClasspath
}

tasks.named('check') {
    dependsOn acceptanceTest
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
}

task testJar(type: Jar, dependsOn: testClasses) {
    classifier 'tests'
    from sourceSets.acceptanceTest.output
}

artifacts {
    archives testJar
}

gradlePlugin {
    testSourceSets sourceSets.acceptanceTest
    plugins {
        javaccPlugin {
            id = 'org.javacc.plugin.javacc-gradle'
            implementationClass = 'org.javacc.plugin.gradle.javacc.JavaccPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/javacc/javaccPlugin'
    vcsUrl = 'https://github.com/javacc/javaccPlugin'
    description = 'A JavaCC plugin for Gradle'
    tags = ['javacc']

    plugins {
        javaccPlugin {
            id = 'org.javacc.plugin.javacc-gradle'
            displayName = 'JavaCC Plugin'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom.withXml {
                asNode().appendNode('name', project.getName())
                asNode().appendNode('description', 'Provides the ability to use JavaCC with Gradle. Compiles JavaCC files to Java.')
                asNode().appendNode('url', 'https://github.com/javacc/javaccPlugin')

                def license = asNode().appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The MIT License (MIT)')
                license.appendNode('url', 'http://opensource.org/licenses/MIT')
                license.appendNode('distribution', 'repo')

                def scm = asNode().appendNode('scm')
                scm.appendNode('url', 'https://github.com/javacc/javaccPlugin')
                scm.appendNode('connection', 'scm:https://github.com/javacc/javaccPlugin.git')
                scm.appendNode('developerConnection', 'scm:git://github.com/javacc/javaccPlugin.git')

                def developer = asNode().appendNode('developers').appendNode('developer')
                developer.appendNode('id', 'jmartel')
                developer.appendNode('name', 'Jonathan Martel')
                developer.appendNode('email', 'jonathan.martel@coglinc.ca')
            }
        }
    }
}

bintray {
    if (!project.hasProperty('bintrayUser')) {
        ext.bintrayUser = ''
    }

    if (!project.hasProperty('bintrayApiKey')) {
        ext.bintrayApiKey = ''
    }

    if (!project.hasProperty('ossrhUsername')) {
        ext.ossrhUsername = ''
    }

    if (!project.hasProperty('ossrhPassword')) {
        ext.ossrhPassword = ''
    }

    if (!project.hasProperty('releaseVersion')) {
        ext.releaseVersion = ''
    }

    if (!project.hasProperty('gpgPassphrase')) {
        ext.gpgPassphrase = ''
    }

    user = bintrayUser
    key = bintrayApiKey

    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'javacc-gradle-plugin'
        desc = 'Provides the ability to use JavaCC via Gradle. If the java plugin is also applied, JavaCompile tasks will depend upon the compileJavacc task.'
        websiteUrl = 'https://github.com/javacc/javaccPlugin'
        issueTrackerUrl = 'https://github.com/javacc/javaccPlugin/issues'
        vcsUrl = 'https://github.com/javacc/javaccPlugin.git'
        licenses = ['MIT']
        labels = ['javacc', 'gradle']
        publicDownloadNumbers = true

        version {
            name = releaseVersion
            released = new Date()
            vcsTag = 'javacc-gradle-plugin-' + releaseVersion
            attributes = ['gradle-plugin': 'org.javacc.plugin.javacc-gradle:org.javacc.plugin:javacc-gradle-plugin']
            gpg {
                sign = true
                passphrase = gpgPassphrase
            }
            mavenCentralSync {
                sync = true
                user = ossrhUsername
                password = ossrhPassword
                close = '1'
            }
        }
    }
}
